	NAME	assembly(16)
	RSEG	CODE(0)

PUBLIC ow_PinStatus
PUBLIC owLevel
PUBLIC owTouchBit		
PUBLIC owTouchReset


;--------------------------------------------------------------------------
#include "iom16.h"

#define   DQ_DDR	 DDRA
#define   DQ_PIN	 PINA
#define   DQ_PORT    PORTA
#define   DQ    7           
; Connect pullup resistor(1K) between DQ and VCC
;--------------------------------------------------------------------------


;--------------------------------------------------------------------------
;  Macro definitions
;--------------------------------------------------------------------------
#define STRONG_DQ	sbi DQ_PORT,DQ
#define NORMAL_DQ	cbi DQ_PORT,DQ
#define CLR_DQ		sbi DQ_DDR, DQ
#define SET_DQ		cbi DQ_DDR, DQ   // The pullup drives line high



;--------------------------------------------------------------------------
; SMALLINT ow_PinStatus(void)
;--------------------------------------------------------------------------
; Read the 1-Wire Net line status
; used to make sure the line is idling high after owAcquire()
;
; Returns: TRUE(1):  Line is high
;          FALSE(0): Line is low, ERROR

ow_PinStatus:
	in	r16, DQ_PIN
	bst r16, DQ			; Store bit DQ of r16 in T flag
	clr r16
	bld r16, 0			; Load T flag into bit 0 of r16
	ret
	
	
;--------------------------------------------------------------------------
; SMALLINT owLevel(int portnum, SMALLINT new_level)
;--------------------------------------------------------------------------
; Set the 1-Wire Net line level.  The values for NewLevel are as follows:
;
; 'portnum'    - number 0 to MAX_PORTNUM-1.  This number is provided to
;                indicate the symbolic port number.
; 'new_level'  - new level defined as
;                MODE_NORMAL     0x00
;                MODE_STRONG5    0x02
;                MODE_PROGRAM    0x04	*
;                MODE_BREAK      0x08	*
;
; Returns:  current 1-Wire Net level
; * Note: MODE_PROGRAM & MODE_BREAK not provided yet

owLevel:
	;portnum not used,  new_level is in R20
	cpi r20, 0x02  
	breq Strong5
	
Normal:
	SET_DQ		; set DDR input, the ext. resistor pulls line high 
	NORMAL_DQ	; set PORT low
	clr r16
	ret			; return new_level;
	
Strong5:
	STRONG_DQ	; set PORT high
	CLR_DQ		; set DDR output, source the line 
	mov R16,R20
	ret			; return new_level;


;--------------------------------------------------------------------------
;SMALLINT owTouchBit(int portnum, SMALLINT sendbit)
;--------------------------------------------------------------------------
; Send 1 bit of communication to the 1-Wire Net and return the
; result 1 bit read from the 1-Wire Net.  The parameter 'sendbit'
; least significant bit is used and the least significant bit
; of the result is the return bit.
;
; 'portnum'    - number 0 to MAX_PORTNUM-1.  This number is provided to
;                indicate the symbolic port number.
; 'sendbit'    - the least significant bit is the bit to send
;
; Returns: 0:   0 bit read from sendbit
;          1:   1 bit read from sendbit
;
;
owTouchBit:
	mov r16, r20	;portnum not used, move sendbit to R16
	; timing critical, so disable interrupts here
	in r17, SREG
	cli
	
	
	tst r16
	brne Write1

Write0:
	CLR_DQ		; Start timeslot
	CLR_DQ		; 
	CLR_DQ		; 
	CLR_DQ		; Keep bus low after 6 us
	rjmp	Delay6
Write1:
	CLR_DQ		; Start timeslot
	CLR_DQ		; 
	CLR_DQ		; 
	SET_DQ		; Release bus after 6 us
	rjmp	Delay6

Delay6:	; delaying 6 cycles:
	ldi  R16, $02
LOOP6:  
	dec  R16
    brne LOOP6
    nop

	; sample DQ line @ 15 uS and put result in T 
	in	r16, DQ_PIN
	bst r16, DQ			; Store bit DQ of r16 in T flag


Delay45:	
	ldi  R16, $0F
LOOP45:
	dec  R16
    brne LOOP45

	SET_DQ				; Release bus
	
	; put sample result (T) in return register
	bld r16, 0			; Load T flag into bit 0 of r16


	; restore interrupt if it was enabled
	out SREG,r17
	
	ret					; return result

;--------------------------------------------------------------------------
;SMALLINT owTouchReset(int portnum)
;--------------------------------------------------------------------------
; Reset all of the devices on the 1-Wire Net and return the result.
;
; 'portnum'    - number 0 to MAX_PORTNUM-1.  This number is provided to
;                indicate the symbolic port number.
;
; Returns: TRUE(1):  presense pulse(s) detected, device(s) reset
;          FALSE(0): no presense pulses detected
;
owTouchReset:
	    
    ; timing critical, so disable interrupts here
	in r17, SREG
	cli
	
	CLR_DQ				; Drive DQ low
    
    ; delaying 480 cycles
    ldi  R16, $A0		
LOOP480:
	dec  R16
    brne LOOP480
    
	SET_DQ;				; Release the bus
    
    ; delaying 70 cycles
    ldi  R16, $17		
LOOP69:
	dec  R16
    brne LOOP69
    nop
    
    ; sample DQ line @ 550 uS and put result in T 
	; get presence detect pulse.
    ; 0 = device(s) present, 1 = no device(s) present
    in	r16, DQ_PIN
	com r16				; invert
	bst r16, DQ         ; Store bit DQ of r16 in T flag


    ; Complete the reset sequence recovery
    ; delaying 410 cycles
    ldi  R16, $88		
LOOP410:
	dec  R16
    brne LOOP410
    nop
	nop

	
    ; check for shortcircuit on the line
    sbic DQ_PIN, DQ
    ; put sample result (T) in return register
	bld r16, 0        ; Load T flag into bit 0 of r16
	
	; restore interrupt if it was enabled
	out SREG,r17
	
	ret					; Return sample presence pulse result

;--------------------------------------------------------------

END
